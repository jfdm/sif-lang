sif solution

Description """Coded Door locks restrict access to internal spaces using a locking mechanism activated using a code."""

> Door locks are physical mechanisms, integrated into doors, used to
> restrict access to the internal space that the door is an entry
> to. Door locks are paired with 'keys' that are used to activate
> (lock) and deactivate (unlock) the lock.  These keys are
> /authentication/ tokens that will unlock the door /iff/ the key can
> interact/activate with the locking mechanism..

Solution "Coded Door Locks" solves accesscontrol in physical {

  > Door locks possess a physical mechanism used to ensure the door is
  > closed.
  Property "Locking Mechanism" {

    > Mechanism is used to ensure that the door remains closed.
    Advantage "Holds the Door" is Satisfied {
	  Affects { Makes protection  by "physical bolt"
	  		  , Helps persistence by "once activated works until deactivated" }}


    > Reliability of the locking mechanism to operate correctly is
    > dependent on the qualit of construction.
	Trait "Reliability of Mechanism" is WeakDen {
	  Affects { SomeNeg reliable    by "poor quality of construction leads to mechanism prone to errors."
	  		  , SomeNeg protection  by "unreliable mechanism may lead to unlocked doors."
			  , SomeNeg persistence by "faulty mechanism may increase probability of unlockable doors."}}

    > Locking mechanisms are known to be susceptible to attacks, 'lock
    > picking'.
    Disadvantage "Lock Picking" is WeakDen {
	  Affects { SomeNeg protection   by "facilitating unauthorised access."
	  		  , SomeNeg persistence by "facilitating unauthorised access."}}

    > Ease of use is dependent upon the complexity of the mechanism
    > and its operation.
	Trait "Complexity of Mechanism Operation" is Unknown {
	  Affects { SomePos effortless, SomeNeg effortless}}

    > It is not clear what the default state of the door should be?
	Trait "Fail Safe or Fail Secure" is Unknown {
	  Affects { SomePos protection,  SomeNeg protection
	  		  , SomePos persistence, SomeNeg persistence}}}

  > The physicality of the solution implies that any problems with the
  > mechanism or key would require the entire system to be replaced.
  Property "Singleton" {
    Disadvantage "Singleton Issues" is WeakDen {
	  Affects { SomeNeg protection }}}

  > Each door locking mechanism is associated with an artefact used to interact with the mechanism.
  Property "Access Token" {

    > A token is required to operate the mechanism.
	Trait "Token is required" is Satisfied {
      Affects { Helps selective by "Need access to token to unlock the door."
			  , Helps awareness by "reminder of what access a person has."
	  }}

    > Tokens are assigned to authorised people.
	Trait "Token Ownership" is Denied {
	  Affects { Hurts persistence by "token is forgotten"
	  		  , Hurts protection  by "token is forgotten"}}

    > Administration of this system requires a locksmith.
    Trait "LockSmith" is Satisfied {
	  Affects { Hurts changable by "requiring a third party to make keys"
	  		  , Hurts selective by "requiring a third party to make keys"
			  , SomePos selective
			  , SomePos changable }}}}
